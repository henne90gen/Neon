program
    statements endoffile

statements
    statement
    statements statement

statement
    new_line
    call new_line
    external_function new_line
    function
    type_declaration
    assignment new_line
    return expression new_line
    definition new_line
    if_statement
    for_statement
    import_statement

import_statement
    import string

for_statement
    for assignment ; expression ; assignment { for_statement_body

for_statement_body
    }
    statements }

if_statement
    if expression { if_statement_body

if_statement_body
    if_statement_else
    statements if_statement_else

if_statement_else
    }
    } else { if_statement_else_body

if_statement_else_body
    }
    statements }

external_function
    extern fun identifier ( external_function_header

external_function_header
    external_function_return
    function_args external_function_return

external_function_return
    )
    ) data_type

function
    fun identifier ( function_header

function_header
    ) function_return
    function_args ) function_return

function_return
    { function_body
    data_type { function_body

function_body
    }
    statements }

function_args
    definition
    function_args , definition

type_declaration
    type identifier { type_body
    type identifier { new_line type_body

type_body
    }
    type_member_declarations }

type_member_declarations
    type_member_declaration
    type_member_declarations type_member_declaration

type_member_declaration
    definition new_line

definition
    array_definition
    variable_definition

array_definition
    data_type [ integer ] identifier

variable_definition
    data_type identifier
    identifier identifier

call
    identifier ( call_header

call_header
    )
    call_args )

call_args
    expression
    call_args , expression

assignment
    definition = expression
    identifier = expression
    identifier [ expression ] = expression

expression
    disjunction

disjunction
    conjunction
    disjunction or conjunction

conjunction
    negation
    conjunction and negation

negation
    relation
    not relation

relation
    sum
    sum < sum
    sum <= sum
    sum == sum
    sum != sum
    sum >= sum
    sum > sum

sum
    term
    sum + term
    sum - term

term
    factor
    term * factor
    term / factor

factor
    boolean
    integer
    float
    string
    array_access
    call
    identifier
    ( expression )

array_access
    identifier [ expression ]
